// Twice feature showcase (grouped output)

fn header(name: string) {
  print("--- " + name + " ---");
  return;
}

fn demo_types_and_casts() {
  header("types and casts");

  type NumOrText = int||string;
  type Row = int[2];
  type Grid = Row[2];

  let s: string = "hello";
  let c = 'A';
  let f = 3.14;
  let n: string;
  let alias_value: NumOrText = "alias works";
  let alias_grid: Grid = {{1, 2}, {3, 4}};

  print(s);
  print(c);
  print(f);
  print(n);

  print(typeof(s));
  print(typeof(c));
  print(typeof(f));
  print(typeof(n));
  print(typeof(alias_value));
  print(typeof(alias_grid));

  print(int(true));
  print(bool(0));
  print(char(66));
  return;
}

fn demo_operators() {
  header("operators");

  let greeting = "Hello, " + "Twice!";
  print(greeting);
  print("x:" + 7);
  print("x:" + 3.5);
  print("x:" + 'A');

  print("Twice"[2]);
  let word = "array";
  print(word[0]);

  let n1 = 1 + 2.5;
  let n2 = 6 - 2.5;
  let n3 = 3 * 2.0;
  let n4 = 7 / 2.0;
  print(n1);
  print(n2);
  print(n3);
  print(n4);

  let mod_i = 7 % 4;
  let mod_f = 7.5 % 2.0;
  let mod_assign = 9;
  mod_assign %= 4;
  print(mod_i);
  print(mod_f);
  print(mod_assign);

  let acc = 10;
  acc += 2;
  acc -= 1;
  acc *= 3;
  acc /= 11;
  print(acc);

  let i = 3;
  i++;
  print(i);
  i--;
  print(i);

  let char_sum = 'A' + '1';
  let char_next = 'A' + 1;
  print(char_sum);
  print(char_next);

  print(true && false);
  print(true || false);
  print(true ^^ false);
  print(5 & 3);
  print(5 | 2);
  print(5 ^ 1);
  print(5 << 1);
  print(5 >> 1);
  return;
}

fn demo_control_flow_and_scopes() {
  header("control flow and scopes");

  let score = 10;
  if (score > 10) {
    print("big");
  } elif (score == 10) {
    print("equal");
  } else {
    print("small");
  };

  let scoped = 1;
  if (true) {
    let scoped = 2;
    print(scoped);
  };
  print(scoped);

  if (true) {
    scoped = 9;
  };
  print(scoped);

  {
    let temp = 77;
    print(temp);

    fn tempFn(x: int) int {
      return x + 1;
    }
    print(tempFn(10));
  }
  return;
}

fn demo_loops() {
  header("loops");

  let while_i = 0;
  while (while_i < 3) {
    while_i++;
  };
  print(while_i);

  let for_sum = 0;
  for (let j = 0; j < 4; j++) {
    for_sum = for_sum + j;
  };
  print(for_sum);

  for (let scoped_i = 0; scoped_i < 2; scoped_i++) {
    let loop_local = scoped_i;
    print(loop_local);
  };

  let control_sum = 0;
  for (let k = 0; k < 6; k++) {
    if (k == 2) {
      continue;
    };
    if (k == 5) {
      break;
    };
    control_sum = control_sum + k;
  };
  print(control_sum);

  let loop_count = 0;
  loop {
    loop_count++;
    if (loop_count == 2) {
      break;
    };
  };
  print(loop_count + " loop count");

  let loop_arr_sum = 0;
  let loop_arr_i = 0;
  while (loop_arr_i < 3) {
    let local_arr = {loop_arr_i, loop_arr_i + 1, loop_arr_i + 2};
    loop_arr_sum += local_arr[0];
    loop_arr_i++;
  };
  print(loop_arr_sum);
  return;
}

fn add(a: int, b: int = 2) int {
  return a + b;
}

fn demo_functions() {
  header("functions");

  print(add(5));
  print(add(3));
  print(add(a = 3, b = 4));
  print(add(3, b = 10));

  fn noop() {
    return;
  }
  print(noop());
  return;
}

fn demo_arrays_unions_tuples() {
  header("arrays unions tuples");

  let arr = {1, 2, 3};
  print(arr.length);
  print(arr.length());
  print(arr[1]);
  arr[1] = 99;
  print(arr[1]);

  let typed: int[3] = {4, 5, 6};
  print(typeof(typed));
  print(typed.length);
  print(typed.length());

  let fixed: int[3] = {7, 8, 9};
  print(typeof(fixed));

  let grid: int[2][2] = {{1, 2}, {2, 3}};
  print(typeof(grid));
  print(grid.length);
  print(grid.length());

  let value: int||string = 1;
  print(typeof(value));
  value = "twice";
  print(typeof(value));
  value = 3;
  if (value == 3) {
    print("union if works");
  };

  let gate: string||int = "x";
  gate = 5;
  if (gate == 5) {
    print("union reassign if works");
  };
  if (typeofValue(gate) == int) {
    print("typeofValue works");
  };

  let arr_or_text: int[3]||string = {1, 2, 3};
  print(typeof(arr_or_text));
  arr_or_text = "array or text";
  print(typeof(arr_or_text));

  let tri: int||string||bool = 1;
  tri = "ok";
  tri = true;
  print(typeof(tri));

  fn maybe_name(flag: bool) string||null {
    if (flag) {
      return "twice";
    };
    return;
  }
  maybe_name(false);
  maybe_name(true);

  let tup: (int, string, bool) = (7, "seven", true);
  print(typeof(tup));
  print(tup.0);
  print(tup.1);
  print(tup.2);

  type Pair = (int, string);
  type MaybePair = Pair||string;
  let maybe_pair: MaybePair = (1, "one");
  print(typeof(maybe_pair));
  print(maybe_pair.0);
  maybe_pair = "no pair";
  print(typeof(maybe_pair));

  let mixed = {1, "two", 3};
  print(typeof(mixed));
  print(mixed.length());
  return;
}

fn demo_null_safe_and_coalesce() {
  header("null-safe and coalesce");

  let maybe_arr: int[3];
  print(maybe_arr?.length);
  print(maybe_arr?.length());
  print(maybe_arr?.length ?? 0);
  print(maybe_arr?.length() ?? 0);

  let arr = {1, 2, 3};
  print(arr?.length);
  print(arr?.length());
  print(arr?.length ?? 0);
  print(arr?.length() ?? 0);
  print(arr?.missing);
  print(arr?.missing());
  print(arr?.missing ?? "no member");
  print(arr?.missing() ?? "no method");

  print(hasField(arr, "length"));
  let field_name: string = "length";
  print(hasField(arr, field_name));
  field_name = "missing";
  print(hasField(arr, field_name));
  print(hasField("abc", "length"));
  print(hasField(1, "length"));

  let a: int;
  print(a ?? 42);
  print(7 ?? 42);
  return;
}

fn main() {
  demo_types_and_casts();
  demo_operators();
  demo_control_flow_and_scopes();
  demo_loops();
  demo_functions();
  demo_arrays_unions_tuples();
  demo_null_safe_and_coalesce();
  return;
}
