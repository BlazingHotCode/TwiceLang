// Twice feature showcase

// typed declarations, inference, null defaults
let s: string = "hello";
let c = 'A';
let f = 3.14;
let n: string; // typed declaration without initializer => null

print(s);
print(c);
print(f);
print(n);

print(typeof(s));
print(typeof(c));
print(typeof(f));
print(typeof(n));

print(int(true));
print(bool(0));
print(char(66));

// string concat and numeric coercion
let greeting = "Hello, " + "Twice!";
print(greeting);
print("x:" + 7);
print("x:" + 3.5);
print("x:" + 'A');
print("Twice"[2]); // string indexing => char ('i')
let word = "array";
print(word[0]);    // 'a'

// mixed numeric math
let n1 = 1 + 2.5;
let n2 = 6 - 2.5;
let n3 = 3 * 2.0;
let n4 = 7 / 2.0;
print(n1);
print(n2);
print(n3);
print(n4);

// modulo and compound assignments
let mod_i = 7 % 4;
let mod_f = 7.5 % 2.0;
let mod_assign = 9;
mod_assign %= 4;
print(mod_i);
print(mod_f);
print(mod_assign);

let acc = 10;
acc += 2;
acc -= 1;
acc *= 3;
acc /= 11;
print(acc);

let i = 3;
i++;
print(i);
i--;
print(i);

// char arithmetic
let char_sum = 'A' + '1';
let char_next = 'A' + 1;
print(char_sum);
print(char_next);

// booleans and bitwise operators
print(true && false);
print(true || false);
print(true ^^ false);
print(5 & 3);
print(5 | 2);
print(5 ^ 1);
print(5 << 1);
print(5 >> 1);

// control flow
let score = 10;
if (score > 10) {
  print("big");
} elif (score == 10) {
  print("equal");
} else {
  print("small");
};

// loops
let while_i = 0;
while (while_i < 3) {
  while_i++;
};
print(while_i);

let for_sum = 0;
for (let j = 0; j < 4; j++) {
  for_sum = for_sum + j;
};
print(for_sum);

let control_sum = 0;
for (let k = 0; k < 6; k++) {
  if (k == 2) {
    continue;
  };
  if (k == 5) {
    break;
  };
  control_sum = control_sum + k;
};
print(control_sum); // 8

let loop_count = 0;
loop {
  loop_count++;
  if (loop_count == 2) {
    break;
  };
};
print(loop_count + " loop count"); // 2

// function calls: before declaration, default args, named args, mixed args
print(add(5));

fn add(a: int, b: int = 2) int {
  return a + b;
}

print(add(3));
print(add(a = 3, b = 4));
print(add(3, b = 10));

// arrays: literals, typed arrays, indexing, mutation, length()
let arr = {1, 2, 3};
print(arr.length());
print(arr[1]);
arr[1] = 99;
print(arr[1]);

let typed: int[3] = {4, 5, 6};
print(typeof(typed));
print(typed.length());

let fixed: int[3] = {7, 8, 9};
print(typeof(fixed));

let grid: int[][2] = {{1}, {2, 3}};
print(typeof(grid));
print(grid.length());

// union types
let value: int||string = 1;
print(typeof(value));
value = "twice";
print(typeof(value));
value = 3;
if (value == 3) {
  print("union if works");
};

let gate: string||int = "x";
gate = 5;
if (gate == 5) {
  print("union reassign if works");
};

if (typeofValue(gate) == int) {
  print("typeofValue works");
};

// array of union values
let mixed = {1, "two", 3};
print(typeof(mixed));   // (int||string)[3]
print(mixed.length());  // 3
